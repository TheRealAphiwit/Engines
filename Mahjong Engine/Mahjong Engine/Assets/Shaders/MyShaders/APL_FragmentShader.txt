#version 410

struct Material {
    sampler2D albedo;
    sampler2D specular;
    sampler2D normal;
    float shininess;
};

uniform Material material;

uniform vec4 eyePosition;
uniform vec4 primaryColorVec4;
uniform vec4 light_ambient;
uniform vec4 light_diffuse;
uniform vec4 light_specular;
uniform vec3 light_position;
uniform vec3 light_attenuation;

in vec3 v_normal;
in vec2 UV_Coord;
in vec3 position;
in vec3 vecToEye;

out vec4 fragColor;

void main() 
{
    // Sample the textures from material
    vec3 albedoTexel = texture(material.albedo, UV_Coord).rgb;
    vec3 specularTexel = texture(material.specular, UV_Coord).rgb;

    vec3 color = vec3(0.0);

    // Ambient
    vec3 ambient = albedoTexel * light_ambient.rgb;
    color += ambient;

    // Diffuse
    vec3 lightDir = normalize(light_position - position);
    vec3 normal = normalize(v_normal);
    float diffIntensity = max(dot(lightDir, normal), 0.0);

    if (diffIntensity > 0.0)
    {
        float dist = length(light_position - position);
        float attenuation = 1.0 / max(0.001, light_attenuation.x + light_attenuation.y * dist + light_attenuation.z * (dist * dist));

        vec3 diffuse = diffIntensity * light_diffuse.rgb * albedoTexel;
        color += diffuse * attenuation;

        // Specular
        vec3 viewDir = normalize(vecToEye);
        vec3 halfVec = normalize(-lightDir + viewDir);
        float specAngle = max(dot(halfVec, normal), 0.0);
        float specIntensity = pow(specAngle, material.shininess);

        vec3 specular = specIntensity * light_specular.rgb * specularTexel;
        color += specular * attenuation;
    }

    fragColor = vec4(color, 1.0);
}