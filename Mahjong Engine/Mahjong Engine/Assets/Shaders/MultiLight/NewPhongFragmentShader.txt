#version 410
#define MAX_LIGHTS 16

// Lights
uniform int lightCount;

const int LIGHT_POINT = 0;
const int LIGHT_DIRECTIONAL = 1;
const int LIGHT_SPOT = 2;

// Material properties
uniform vec4 materialDiffuse;
uniform vec4 materialSpecular;
uniform float materialShininess;
uniform sampler2D diffuseMap;

// Input from vertex shader
in vec3 v_normal;
in vec2 UV_Coord;
in vec3 position;
in vec3 vecToEye;

struct Light 
{
    int Type; // 0 = point, 1 = directional, 2 = spot
    vec3 Position;
    vec3 Direction;
    vec3 Color;
    float Intensity;
    float Range;
    float InnerCone;
    float OuterCone;
};

void main()
{
    vec4 texel = texture(diffuseMap, UV_Coord);
    vec3 N = normalize(v_normal);
    vec3 V = normalize(vecToEye);

    vec3 result = vec3(0.0);

    // Ambient (global, cheap) - ambient is hardcoded right now with a set value
    vec3 ambient = texel.rgb * 0.05;
    result += ambient;

    for (int i = 0; i < lightCount && i < MAX_LIGHTS; ++i)
    {
        Light light = lights[i];

        vec3 L;
        float attenuation = 1.0;

        // --- Light direction & attenuation ---
        if (light.Type == LIGHT_POINT || light.Type == LIGHT_SPOT)
        {
            vec3 toLight = light.Position - position;
            float distance = length(toLight);
            L = normalize(toLight);

            attenuation = clamp(1.0 - distance / light.Range, 0.0, 1.0);
        }
        else // Directional
        {
            L = normalize(-light.Direction);
        }

        // --- Diffuse ---
        float NdotL = max(dot(N, L), 0.0);
        if (NdotL <= 0.0)
            continue;

        vec3 diffuse = NdotL * light.Color * light.Intensity * materialDiffuse.rgb;

        // --- Specular (Blinn-Phong) ---
        vec3 H = normalize(L + V);
        float spec = pow(max(dot(N, H), 0.0), materialShininess);
        vec3 specular = spec * materialSpecular.rgb;

        // --- Spotlight cone ---
        if (light.Type == LIGHT_SPOT)
        {
            float theta = dot(L, normalize(-light.Direction));
            float epsilon = max(light.InnerCone - light.OuterCone, 0.001);
            float coneFactor = clamp((theta - light.OuterCone) / epsilon, 0.0, 1.0);
            attenuation *= coneFactor;
        }

        result += (diffuse + specular) * attenuation * texel.rgb;
    }

    fragColor = vec4(result, texel.a);
}